buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

repositories {
    mavenCentral()
    maven { // Mantle
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'java'
apply plugin: 'maven-publish'

// External properties
ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.version}.${config.build_number}"
ext.full_version = "${config.mc_version}-${project.version}"
group = 'pkanti.healthscoring' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'healthscoring'

ext.tempBuildDir = 'build/sources/java'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: config.mappings_version
    accessTransformer = file('src/main/resources/META-INF/hs_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.mc_version}-${config.forge_version}"

    compile "slimeknights.mantle:Mantle:${config.mc_version_mantle}-${config.mantle_version}"
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': config.mc_version, 'mantleversion': config.mantle_version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes([
            "FMLAT": "hs_at.cfg",
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar') 
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}


sourceSets {
    main {
        output.resourcesDir = output.classesDir // mcmod.info in debug build
    }
}

// FOR POSTERITY
// The following token replacement took me a few evenings to figure out
// Issues I came across:
// 1. Never imported ReplaceTokens (IntelliJ installed ant for me so I had no idea)
// 2. Modified files weren't compiled (manually set compileJava.source)
// 3. expand vs ReplaceTokens (works the same but expand doesn't require an import (doesn't matter much))
// 4. Very kind MinecraftForge questions and one angry dragon

task updateVersion(type: Copy) {
    from (sourceSets.main.java.srcDirs) { exclude 'mcmod.info' }
    into project.tempBuildDir
    expand "GRADLE_VERSION": project.full_version
}

compileJava.source = files(project.tempBuildDir)
compileJava.dependsOn(updateVersion)

task incrementBuildNumber {
    doFirst {
        config.build_number = (config.build_number.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

defaultTasks 'clean', 'build', 'incrementBuildNumber'